// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    classpath 'com.android.tools.build:gradle:2.0.0-beta6'
    classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }

  configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
  repositories {
    jcenter()
  }

  apply plugin: 'checkstyle'
  //for java modules
  checkstyle {
    toolVersion = "6.15"
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  }

  //for android modules
  task checkstyle(type: Checkstyle) {
    configFile file("../config/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath =
        file("../config/checkstyle/checkstyle-suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
  }

  apply plugin: 'pmd'

  task pmd(type: Pmd) {
    ruleSetFiles = files("${project.rootDir}/config/pmd/ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
      xml.enabled = false
      html.enabled = true
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}